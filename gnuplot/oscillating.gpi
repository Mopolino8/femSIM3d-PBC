# Gnuplot script file for plotting data in file "vk-???.dat"
set   autoscale                        # scale axes automatically
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtic auto                          # set xtics automatically
set ytic auto                          # set ytics automatically
#set title "Oscillating drop" 
set xlabel "time (t)" font "Times,25"
set ylabel "amplitude (a_0)" font "Times,25" offset 1,0
set label
set xrange [0.0:3.0]
set xtics 0.20
set format  "%1.1f"
set yrange [0.988:1.012]
set ytics 0.01
set format y "%1.2f"
unset grid
set size ratio 0
set key bottom right
set key box linestyle 1

#--------------------------------------------------
# set terminal postscript enhanced font "Times-Roman,23"
# set output '| ps2pdf - oscillating.pdf'
#-------------------------------------------------- 

# parameters
n=2
sigma=0.1
R=0.5

# fluid properties
mu=0.001
rho=1
rho_l=1
rho_g=0.001

# initial condition
a0=0.01
y0=1.000

nu=mu/rho
tao=R/(5*nu)
w1=sqrt( 24*sigma/( (3*rho_l+2*rho_g)*R*R*R) )
f(x) = y0 + a0 * exp(-x/tao) * cos(w1*x)
yt1(x)= y0 + a0 * exp(-x/tao)
yt2(x)= y0 - a0 * exp(-x/tao)

fileObj = "../dat/diameter.dat"

plot fileObj using 1:($4-0.002208) every 3 title 'numeric' \
     with points ls 4,\
 	 f(x) title 'analytic' with lines ls 1
