# Gnuplot script file for plotting data in file "vk-???.dat"
set   autoscale                        # scale axes automatically
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtic auto                          # set xtics automatically
set ytic auto                          # set ytics automatically
#set title "Velocity profiles of rotating disk" 
#set xlabel "{/Symbol x}" font "Times,20"

set xlabel "z" font "Times,20" offset 0,0.5
set xtics font "Times,20" 

set ylabel "F,G,H,c" font "Times,20" offset 2,0
set ytics font "Times,20" 

set label
set xrange [0.0:10.0]
set yrange [-0.1:1.1]
set xtics 2
set ytics 0.5

# set grid xtics mxtics ytics mytics back ls 12 ls 13
set key box center right
set key box ls -1 width -7 spacing 0.7
set key samplen 4
set key font ",19"
set grid
#set arrow 1 from 0.61,0.16 to 0.51,0.28
#set arrow 2 from 0.80,0.57 to 1.10,0.65 
#set arrow 3 from 1.97,0.55 to 2.22,0.48
#set arrow 4 from 0.08,0.50 to 0.35,0.55

set style line 1 lt 1 lw 4
set style line 2 pointsize 1.5 lt 4 lw 2 
set style line 3 pointsize 1.5 lt 6 lw 2 
set style line 4 pointsize 1.5 lt 14 lw 2 
set style line 5 pointsize 1.5 lt 10 lw 2 

#set term dumb

set terminal postscript enhanced font "Times-Roman,25"
#set term png 
#set terminal pdf

fileObj = "../sim/vk2."

#fileObjAnalytic = "`echo $DATA_DIR`/baseState/nuCte/analiticoNuCte.dat"
#fileObjAnalytic = "`echo $DATA_DIR`/baseState/nuZ/analiticoNuZ.dat"
fileObjAnalytic = "`echo $DATA_DIR`/baseState/nuC/Sc2000/analiticoNuC.dat"

do for [N=1:290]{
 set output sprintf('nuC%03.0f.eps',N)

 plot fileObj .N using 1:2 every 1 title 'F' ls 2 with points,\
      fileObj .N using 1:3 every 1 title 'G' ls 3 with points,\
	  fileObj .N using 1:4 every 1 title 'H' ls 4 with points,\
	  fileObj .N using 1:5 every 1 title 'C' ls 5 with points,\
	  fileObjAnalytic using 1:2 title 'F,G,H,c-analytical' ls 1 with lines ,\
	  fileObjAnalytic using 1:3 notitle ls 1 with lines ,\
	  fileObjAnalytic using 1:4 notitle ls 1 with lines ,\
	  fileObjAnalytic using 1:5 notitle ls 1 with lines
  print N
}
